
&После("ПриЗаписи")
Процедура Работа_С_Заказами_ПриЗаписи(Отказ, Замещение)
	
	Регистратор = ЭтотОбъект.Отбор.Регистратор.Значение;
	Если ТипЗнч(Регистратор) <> Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
	   И ТипЗнч(Регистратор) <> Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
	   И ТипЗнч(Регистратор) <> Тип("ДокументСсылка.ОперацияПоПлатежнойКарте") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств") Тогда
		Если Не Регистратор.ПроведеноБанком Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Регистратор) = Тип("ДокументСсылка.ОперацияПоПлатежнойКарте") Тогда
		Если Не Регистратор.ОплатаВыполнена Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Если МенеджерВременныхТаблиц.Таблицы.Найти("ТаблицаРасшифровкаПлатежа") = Неопределено Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	РасшифровкаПлатежа.Заказ        			КАК Заказ,
		|	&Регистратор                            	КАК Регистратор,
		|	РасшифровкаПлатежа.Сумма        			КАК Сумма,
		|	РасшифровкаПлатежа.СуммаВзаиморасчетов		КАК СуммаВзаиморасчетов,
		|	РасшифровкаПлатежа.СтавкаНДС    			КАК СтавкаНДС,
		|	РасшифровкаПлатежа.СуммаНДС     			КАК СуммаНДС,
		|	СостоянияЗаказовКлиентов.Состояние			КАК Состояние,
		|	СостоянияЗаказовКлиентов.ДатаСобытия		КАК ДатаСобытия,
		//|	СостоянияЗаказовКлиентов.СуммаОплаты		КАК СуммаОплаты,
		|	ЕСТЬNULL(СостоянияЗаказовКлиентов.СуммаОплаты,0) КАК СуммаОплаты,
		|	СостоянияЗаказовКлиентов.ПроцентОплаты		КАК ПроцентОплаты,
		|	СостоянияЗаказовКлиентов.СуммаОтгрузки		КАК СуммаОтгрузки,
		|	СостоянияЗаказовКлиентов.ПроцентОтгрузки	КАК ПроцентОтгрузки,
		|	СостоянияЗаказовКлиентов.СуммаДолга			КАК СуммаДолга,
		|	СостоянияЗаказовКлиентов.ПроцентДолга		КАК ПроцентДолга,
		|	СостоянияЗаказовКлиентов.ЕстьРасхожденияОрдерНакладная КАК ЕстьРасхожденияОрдерНакладная
		|	ИЗ
		|		Документ.%ТипДокумента%.РасшифровкаПлатежа КАК РасшифровкаПлатежа
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.СостоянияЗаказовКлиентов КАК СостоянияЗаказовКлиентов
		|	ПО
		|		РасшифровкаПлатежа.Заказ = СостоянияЗаказовКлиентов.Заказ
		|	ГДЕ
		|		РасшифровкаПлатежа.Заказ <> НЕОПРЕДЕЛЕНО
		|		И РасшифровкаПлатежа.Заказ ССЫЛКА Документ.ЗаказКлиента
		|		И РасшифровкаПлатежа.Ссылка = &Регистратор
		|		И РасшифровкаПлатежа.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
		|");
		
		Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
			ТипДокумента = "ПриходныйКассовыйОрдер";
		ИначеЕсли ТипЗнч(Регистратор) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств") Тогда
			ТипДокумента = "ПоступлениеБезналичныхДенежныхСредств";
		ИначеЕсли ТипЗнч(Регистратор) = Тип("ДокументСсылка.ОперацияПоПлатежнойКарте") Тогда
			ТипДокумента = "ОперацияПоПлатежнойКарте";
		Иначе
			Возврат;
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ТипДокумента%", ТипДокумента);
		
	Иначе	
	
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	РасшифровкаПлатежа.Заказ        			КАК Заказ,
		|	&Регистратор                            	КАК Регистратор,
		|	РасшифровкаПлатежа.Сумма        			КАК Сумма,
		|	РасшифровкаПлатежа.СуммаВзаиморасчетов		КАК СуммаВзаиморасчетов,
		|	РасшифровкаПлатежа.СтавкаНДС    			КАК СтавкаНДС,
		|	РасшифровкаПлатежа.СуммаНДС     			КАК СуммаНДС,
		|	СостоянияЗаказовКлиентов.Состояние			КАК Состояние,
		|	СостоянияЗаказовКлиентов.ДатаСобытия		КАК ДатаСобытия,
		//|	СостоянияЗаказовКлиентов.СуммаОплаты		КАК СуммаОплаты,
		|	ЕСТЬNULL(СостоянияЗаказовКлиентов.СуммаОплаты,0) КАК СуммаОплаты,
		|	СостоянияЗаказовКлиентов.ПроцентОплаты		КАК ПроцентОплаты,
		|	СостоянияЗаказовКлиентов.СуммаОтгрузки		КАК СуммаОтгрузки,
		|	СостоянияЗаказовКлиентов.ПроцентОтгрузки	КАК ПроцентОтгрузки,
		|	СостоянияЗаказовКлиентов.СуммаДолга			КАК СуммаДолга,
		|	СостоянияЗаказовКлиентов.ПроцентДолга		КАК ПроцентДолга,
		|	СостоянияЗаказовКлиентов.ЕстьРасхожденияОрдерНакладная КАК ЕстьРасхожденияОрдерНакладная
		|	ИЗ
		|		ТаблицаРасшифровкаПлатежа КАК РасшифровкаПлатежа
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.СостоянияЗаказовКлиентов КАК СостоянияЗаказовКлиентов
		|	ПО
		|		РасшифровкаПлатежа.Заказ = СостоянияЗаказовКлиентов.Заказ
		|	ГДЕ
		|		РасшифровкаПлатежа.Заказ <> НЕОПРЕДЕЛЕНО
		|		И РасшифровкаПлатежа.Заказ ССЫЛКА Документ.ЗаказКлиента
		|");
		
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	ТаблицаОбъектовОплаты = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаОбъектовОплаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	О_Работа_С_Заказами.ОбработатьСобытиеОплатыЗаказаКлиента(ТаблицаОбъектовОплаты, ДополнительныеСвойства);
	
КонецПроцедуры
